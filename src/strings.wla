; impatience <github.com/kroc/impatience> copyright (c) Kroc Camen 2020-2022:
; creative commons attribution (cc-by) 4.0, see LICENSE.txt
;
; macro to write a length-prefixed string:
;
.MACRO  .str    ARGS    _str
;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
        .BYTE   _str.length     ; don't ASCII-map the string-length!!
        .ASC    _str            ; convert string bytes to tilemap bytes
;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
.ENDM


printStr:
;===============================================================================
; print a length-prefixed string:
;
; in:   DE      string address
;       HL      VRAM (tile-map) address
;-------------------------------------------------------------------------------
        ld      A,      [DE]            ; first byte is string length (1-based)
        ld      C,      A               ; move this to C
        inc     DE                      ; skip over this byte

        ;-----------------------------------------------------------------------
@char:  ld      A,      [DE]            ; read a character in
        ld      [HL+],  A               ; write the character to display

        dec     C                       ; decrease remaining character count
        ret     z                       ; when zero, exit

        inc     DE                      ; move to the next character
        jr      @char                   ; continue...
