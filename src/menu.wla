; impatience <github.com/kroc/impatience> copyright (c) Kroc Camen 2020-2022:
; creative commons attribution (cc-by) 4.0, see LICENSE.txt
;
; menu-items:
;
;       +--------------------+
;       | IMPATIENCE         | <- current game name
;       |                    |
;       |   CHANGE GAME      | <- change between games Aces Up, Klondike &c.
;       |                    |
;       |   RESTART GAME     | <- replay the game using the same cards
;       |                    |
;       |   OPTIONS          | < - configuration, about &c.
;       |                    |
;       +--------------------+
;
.DEF    MENU_WIDTH      TILEMAP_WIDTH
.DEF    MENU_HEIGHT     10
.DEF    MENU_ADDR       tilemap.front_buffer


clear:
;===============================================================================
; clears the menu, setting it back to its default display:
;
;-------------------------------------------------------------------------------
        ; (menu background tile)
        ld      A,      TILE_MENU_BG
        ; the GameBoy screen is 20x18 tiles, but VRAM contains a 32x32
        ; scrollable region. since the tableu may need to scroll a bit,
        ; we place the menu above the playfield
        ld      HL,     tilemap.front_buffer

        ; even though the screen is 20 out of 32 tiles wide, we fill the whole
        ; width to ensure the menu area remains white if the tableu scrolls
        ;
        ; TODO: 10 rows is 320 tiles, so call fill (256), plus 64 remainder.
        ;       we should calculate this at assemble-time
        call    fill
        call    fill@x64

        ; print the title:
        ld      HL,     MENU_ADDR + (1 * TILEMAP_WIDTH) + 1
        ld      DE,     @impatience
        call    print_str

        ; change game:
        ld      HL,     MENU_ADDR + (3 * TILEMAP_WIDTH) + 3
        ld      DE,     @change_game
        call    print_str

        ; restart game:
        ld      HL,     MENU_ADDR + (5 * TILEMAP_WIDTH) + 3
        ld      DE,     @restart_game
        call    print_str

        ; options:
        ld      HL,     MENU_ADDR + (7 * TILEMAP_WIDTH) + 3
        ld      DE,     @options
        call    print_str

        ret


; strings:
;===============================================================================
@impatience:
        .str    "impatience"
@change_game:
        .str    "change game"
@restart_game:
        .str    "restart game"
@options:
        .str    "options"