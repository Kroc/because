; impatience <github.com/kroc/impatience> copyright (c) Kroc Camen 2020:
; creative commons attribution (cc-by) 4.0, see LICENSE.txt
;
.CODE

init:
;===============================================================================
        call    gfx.wait_for_vblank
        
        ; temp: set scroll offset to move menu off-screen
        ld      A,      MENU_WIDTH * 8
        ld      [SCX],  A
        xor     A
        ld      [SCY],  A

        ; setup the playfield for the game
        ;-----------------------------------------------------------------------
        ; completely clear the table of cards, both the piles and the storage
        ; pool of all cards on the table. DE will be set to the beginning
        ; of the storage pool, as needed by the allocation routines
        ;
        call    cards.clear

        ; allocate the deck:
        ld      B,      2               ; row
        ld      C,      MENU_WIDTH + 1  ; column
        call    cards.alloc_deck

        ; allocate the tableau:
        ld      HL,     cards.tableau1
        ld      C,      MENU_WIDTH + 5  ; column, row is re-used
        ld      A,      13              ; max. number of cards
        call    cards.alloc_pile

        ; tableau 2, 3 & 4 all follow in order,
        ; so we do not need to set HL again
        ld      C,      MENU_WIDTH + 8  ; column, row is re-used
        call    cards.alloc_pile

        ld      C,      MENU_WIDTH + 11 ; column, row is re-used
        call    cards.alloc_pile

        ld      C,      MENU_WIDTH + 14 ; column, row is re-used
        call    cards.alloc_pile

        ;-----------------------------------------------------------------------
        call    playfield.draw_deck
        
        ; NOTE: tableau 1, 2, 3 & 4 follow each other, so each call
        ;       to `draw_pile` moves HL on to the next tableau pile
        ld      HL,     cards.tableau1
        call    playfield.draw_pile     ; draw tableau 1
        call    playfield.draw_pile     ; draw tableau 2
        call    playfield.draw_pile     ; draw tableau 3
        call    playfield.draw_pile     ; draw tableau 4

        ;-----------------------------------------------------------------------
        call    gfx.screen_off
        call    gfx.refresh_vram
        call    gfx.screen_on
        ret