; impatience <github.com/kroc/impatience> copyright (c) Kroc Camen 2020-2022:
; creative commons attribution (cc-by) 4.0, see LICENSE.txt
;
; create template card piles for the intial setup of the game:
;
;       .---.   .---. .---. .---. .---.
;       |DCK|   | D | | D | | D | | D |
;       |   |   | 1 | | 2 | | 3 | | 4 |
;       '---'   '---' '---' '---' '---'
;       .---.
;       |DSC|
;       |   |
;       '---'
;
piles.Begin
        ; label,                type, max-cards,
        ; row,                  col,
        ; pile-left,            pile-right,
        ; pile-up,              pile-down
piles.Define \
        acesup_deck,            PILE_DECK, 52, \
        1,                      TABLEAU_LEFT + 1, \
        INDEX_DEPOT4,           INDEX_DEPOT1, \
        INDEX_DECK,             INDEX_DECK
piles.Define \
        acesup_discard,         PILE_DISCARD, 52, \
        7,                      TABLEAU_LEFT + 1, \
        INDEX_DEPOT4,           INDEX_DEPOT1, \
        INDEX_DECK,             INDEX_DISCARD
piles.Define \
        acesup_depot1,          PILE_COLUMN | VALID_ACESUP, 13, \
        1,                      TABLEAU_LEFT + 5, \
        INDEX_DECK,             INDEX_DEPOT2, \
        INDEX_DEPOT1,           INDEX_DEPOT1
piles.Define \
        acesup_depot2,          PILE_COLUMN | VALID_ACESUP, 13, \
        1,                      TABLEAU_LEFT + 8 \
        INDEX_DEPOT1,           INDEX_DEPOT3, \
        INDEX_DEPOT2,           INDEX_DEPOT2
piles.Define \
        acesup_depot3,          PILE_COLUMN | VALID_ACESUP, 13, \
        1,                      TABLEAU_LEFT + 11 \
        INDEX_DEPOT2,           INDEX_DEPOT4, \
        INDEX_DEPOT3,           INDEX_DEPOT3
piles.Define \
        acesup_depot4,          PILE_COLUMN | VALID_ACESUP, 13, \
        1,                      TABLEAU_LEFT + 14 \
        INDEX_DEPOT3,           INDEX_DECK, \
        INDEX_DEPOT4,           INDEX_DEPOT4


init:
;===============================================================================
        rst     rst_waitVBlank
        
        ; temp: set scroll offset to move menu off-screen
        ld      A,      MENU_WIDTH * 8
        ldh     [<SCX], A
        xor     A
        ldh     [<SCY], A

        ; setup the tableau layout for the game
        ;-----------------------------------------------------------------------
        ; completely clear the table of cards, both the piles
        ; and the storage pool of all cards on the table
        ;
        call    piles.clear

        ld      BC,     acesup_deck
        call    piles.allocateDeck      ; allocate the deck,
        call    piles.allocate          ; and discard pile

        ; allocate the depots:
        ld      L,      INDEX_DEPOT1    ; (skip `INDEX_HAND`)
        call    piles.allocate
        call    piles.allocate          ; depot 2, 3 & 4 all follow in order,
        call    piles.allocate          ; so we don't need to set HL & BC again
        call    piles.allocate

        ;-----------------------------------------------------------------------
        ld      L,      INDEX_DEPOT1
        ld      A,      CARD_J | SUIT_CLUB
        call    piles.addCard
        ld      A,      CARD_3 | SUIT_SPADE
        call    piles.addCard

        ld      L,      INDEX_DEPOT2
        ld      A,      CARD_X | SUIT_HEART
        call    piles.addCard
        ld      A,      CARD_4 | SUIT_SPADE
        call    piles.addCard

        ld      L,      INDEX_DEPOT3
        ld      A,      CARD_2 | SUIT_SPADE
        call    piles.addCard
        ld      A,      CARD_6 | SUIT_CLUB
        call    piles.addCard

        ld      L,      INDEX_DEPOT4
        ld      A,      CARD_A | SUIT_SPADE
        call    piles.addCard
        ld      A,      CARD_3 | SUIT_DIAMOND
        call    piles.addCard

        ;-----------------------------------------------------------------------
        call    tableau.refresh
        rst     rst_screenOff
        call    tilemap.refresh
        rst     rst_screenOn

        ; move the cursor to the bottom card in the first depot pile
        ld      L,      INDEX_DEPOT1
        jp      cursor.pointToPile