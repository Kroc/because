; impatience <github.com/kroc/impatience> copyright (c) Kroc Camen 2020-2022:
; creative commons attribution (cc-by) 4.0, see LICENSE.txt
;

; the GameBoy screen is 20x18 tiles, but VRAM contains a 32x32 playfield.
; for our purposes we place the menu on the left and the playfield next to it,
; therefore the playfield begins at X = 12 and not the top-left (X = 0)
PLAYFIELD_TOP   = 0
PLAYFIELD_LEFT  = MENU_WIDTH
PLAYFIELD_ADDR  = tilemap.front_buffer + (PLAYFIELD_TOP * 32) + PLAYFIELD_LEFT

.RAMSECTION \
        "cursor"        RETURNORG
        ;-----------------------------------------------------------------------
        ; the address of the Pile struct that the cursor is pointing to;
        ; e.g. deck / tableau; $0000 if cursor is free
        cursor_pile     WORD
        ; the number of the card in the pile the cursor is pointing at
        cursor_card     BYTE
.ENDS

clear:
;===============================================================================
; clears the playfield area, i.e. where cards are played:
;
;-------------------------------------------------------------------------------
        ; the background tile indices are 'signed', so what we think of as
        ; the first tile, tile "0", is tile # "-1", i.e $80
        ld      A,      $80
        ld      HL,     PLAYFIELD_ADDR
        ; number of tiles to skip to next row
        ld      DE,     32-20

        ;-----------------------------------------------------------------------
        ld      C,      32      ; 32 rows
@row:   ld      B,      20      ; 20 columns (13...32)
@col:   ld      [HL+],  A       ; set cell to empty background tile
        dec     B
        jr      nz, @col
        
        ; we only clear 20 out of 32 columns (menu area is preserved),
        ; so move to the next row by skipping ahead over the menu
        add     HL,     DE

        dec     C
        jr      nz, @row

        ret


drawDeck:
;===============================================================================
; draws the deck on the front-buffer:
;
;-------------------------------------------------------------------------------
        ld      HL,     cards.deck.pool ; address of deck pile
        ld      D,      [HL]            ; read cards-address, lo-byte
        inc     L
        ld      E,      [HL]            ; read cards-address, hi-byte
        inc     L
        ld      B,      [HL]            ; read row
        inc     L
        ld      C,      [HL]            ; read column

        ; NOTE: by incrementing L here, we have automatically moved onto the
        ;       next Pile structure. by returning this HL value, the caller
        ;       can draw multiple piles that follow one another (e.g tableaus)
        inc     L
        inc     L
        inc     L
        inc     L
        push    HL

        ; draw deck:
        ;-----------------------------------------------------------------------
        ; read the card-count from the first-byte in the pile's card pool.
        ; if the deck is empty, draw its outline instead
        ;
        ld      A,      [DE]            ; number of cards in pile
        and     A,      A               ; (set flags!)
        jr      z,      drawEmpty       ; if zero, draw a placeholder
        
        ; the deck in particular extends a row upwards
        ; to give it that thicker appearance
        dec     B
        ; get an address in the front-buffer for the X/Y co-ords
        call    tilemap.getBufferAddr
        ; draw out the tiles for the deck
        ld      DE,     tilemap.tilemap_deck     
        call    tilemap.writeTiles_3wide_5rows

        ;-----------------------------------------------------------------------
        ; return the beginning address of the next Pile structure
        pop     HL
        ret


drawEmpty:
;===============================================================================
; draw a pile placeholder, an outline where cards may go:
;
;-------------------------------------------------------------------------------
        call    tilemap.getBufferAddr
        ld      DE,     tilemap.tilemap_empty
        call    tilemap.writeTiles_3wide_4rows

        ;-----------------------------------------------------------------------
        pop     HL
        ret


drawPile:
;===============================================================================
; draws a pile on the back-buffer:
;
; in:   HL      address of the Pile structure
;
; out:  HL      HL is moved forward to the next Pile structure
;-------------------------------------------------------------------------------
        
        ; TODO: use `cards.getPilePos` here?
        ;       although we also need the pool address
        ;
        inc     HL                      ; skip over the max-cards byte
        ld      D,      [HL]            ; read cards-address, hi-byte
        inc     L
        ld      E,      [HL]            ; read cards-address, lo-byte
        inc     L
        ld      B,      [HL]            ; read row
        inc     L
        ld      C,      [HL]            ; read column 

        ; NOTE: by incrementing L here, we have automatically moved onto the
        ;       next Pile structure. by returning this HL value, the caller
        ;       can draw multiple piles that follow one another (e.g tableau)
        inc     L
        inc     L
        inc     L
        inc     L
        push    HL

        ; draw pile:
        ;-----------------------------------------------------------------------
        ; read the card-count from the first-byte in the pile's card pool
        ld      A,      [DE]
        and     A,      A               ; (set flags!)
        ; if the pile is empty, draw its outline instead
        jr      z,      drawEmpty
        ; get front-buffer address for the X/Y co-ords. note that this returns
        ; the address in HL, but clobbers BC. we depend upon it preserving A
        ; (our card count) to be able to set BC afterwards!
        call    tilemap.getBufferAddr
        ; retain the card-count for iterating through the pile
        ld      C,      A
        ; begin the pile at the first card
        ld      B,      0

        ; when a pile has multiple cards, the cards below only have their
        ; top row drawn, but the last card has to be drawn whole; the carry
        ; flag indicates this to the `draw_card` routine
        ;
-       inc     B                       ; (pre-increment our counter)
        ld      A,      B               ; compare current & total cards --
        cp      C                       ; carry is set, except the last card!
        push    BC                      ; (preserve when drawing)

        inc     DE                      ; move to next card in pile
                                        ; NOTE: does not change carry!
        ld      A,      [DE]            ; read a card value
        push    DE                      ; (preserve when drawing)

        call    drawCard
        pop     DE
        pop     BC
        jr      c,      -

        ;-----------------------------------------------------------------------
+       pop     HL                      ; return next Pile address
        ret


drawCard:
;===============================================================================
; draws a card, handling all of its different properties
;
; in:   HL      address in the front-buffer to draw the card
;       A       the card to draw
;       carry   if carry is set, only the first row will be drawn
;               (this is for drawing down-stacking piles)
;
; out:  carry   (preserved)
;       BC, DE  (clobbered)
;
; TODO: inactive
;-------------------------------------------------------------------------------
        bit     7,      A               ; is the card face-down?
        jr      z,      @face           ; if not, skip onto drawing face-up

        ; face-down:
        ;-----------------------------------------------------------------------
        ; face-down cards do not differ between active & inactive
        ld      DE,     tilemap.tilemap_back
        ; if carry is set, we want to draw only one row
        jp      c,      tilemap.writeTiles_3wide_1row
        ; otherwise draw the full card back
        jp              tilemap.writeTiles_3wide_4rows

        ; face-up:
        ;-----------------------------------------------------------------------
        ; remember the state of the carry flag;
        ; if set, we draw only the first row
@face:  push    AF
        ld      BC,     32-3            ; amount to skip to next row
        
        ; top-left corner: ordinal
        ld      D,      A               ; make a copy of the original value
        and     %00001111               ; trim out the other bits
        add     TILE3X4_CARD_1X1-1      ; offset the tile with the ordinal
        ld      [HL+],  A               ; write the tile and advance

        ; top-middle:
        ld      A,      TILE3X4_CARD_1X2
        ld      [HL+],  A

        ; top-right corner: suit
        ld      A,      D
        swap    A                       ; move the suit to the bottom nybble
        and     %00000011               ; mask out the other bits
        ld      E,      A               ; remember the suit for later too
        add     TILE3X4_CARD_1X3
        ld      [HL+],  A
        add     HL,     BC              ; move to the next row

        ; should we only draw the first row?
        pop     AF                      ; restore carry
        ret     c                       ; return now if carry-set

        ; middle:
        ld      A,      TILE3X4_CARD_2X1
        ld      [HL+],  A
        ld      A,      TILE3X4_CARD_2X2
        ld      [HL+],  A
        ld      A,      TILE3X4_CARD_2X3
        ld      [HL+],  A
        add     HL,     BC              ; move to the next row
        ld      A,      TILE3X4_CARD_2X1
        ld      [HL+],  A
        ld      A,      TILE3X4_CARD_2X2
        ld      [HL+],  A
        ld      A,      TILE3X4_CARD_2X3
        ld      [HL+],  A
        add     HL,     BC              ; move to the next row

        ; bottom-left: suit
        ld      A,      E               ; retrieve suit from earlier
        add     TILE3X4_CARD_4X1
        ld      [HL+],  A

        ; bottom-middle:
        ld      A,      TILE3X4_CARD_4X2
        ld      [HL+],  A

        ; bottom-right: ordinal
        ld      A,      D               ; retrieve ordinal from earlier
        and     %00001111               ; trim out the other bits
        add     TILE3X4_CARD_4X3-1      ; offset the tile with the ordinal
        ld      [HL+],  A               ; write the tile and advance

        ret


pointToCard:
;===============================================================================
; move the cursor to a specific card in a pile:
;
; in:   HL      address of the Pile structure
;       A       number of the card in the pile, 0-based
;
; out:  B       row number of the card (not pixels!)
;       C       column number of the card (not pixels!)
;       A, DE   (clobbered)
;-------------------------------------------------------------------------------
        ; update the record of which pile/card is currently being pointed to.
        ; if a pile has multiple cards, then pushing up/down will go through
        ; the cards before leaving the pile
        ;
        ld      DE,     cursor_pile
        push    AF                      ; A holds the card number to point to

        ; save the address of the pile to point to
        ld      A,      H
        ld      [DE],   A
        inc     DE
        ld      A,      L
        ld      [DE],   A
        inc     DE
        ; save the card number to point to
        pop     AF
        ld      [DE],  A

        ; retrieve the row/column for the bottom (top, visually) of the pile
        call    cards.getPilePos
        
        ; add the card number to the row number as
        ; each card in a pile adds another tile's height
        add     A,      B
        ld      B,      A

        ; in the case of cards, we want to point to the space
        ; between the suit / number so as not to obscure either
        inc     C

        jp      cursor.moveToPos


getCursorPile:
;===============================================================================
; returns the address of the Pile struct the cursor is pointing to:
;
; out:  HL      ; address of the Pile the cursor is pointing at
;       A       ; card number being pointed to
;       DE      ; (clobbered)
;-------------------------------------------------------------------------------
        ; retrieve current pile location into DE
        ld      HL,     cursor_pile
        ld      D,      [HL]
        inc     HL
        ld      E,      [HL]
        inc     HL
        ; retrieve card number being pointed to
        ld      A,      [HL]
        ld      H,      D
        ld      L,      E
        ret


moveCursorDown:
;===============================================================================
; move the cursor down, either going through a pile of cards,
; or downward to the nearest pile below the current one
;-------------------------------------------------------------------------------
        call    getCursorPile           ; retrieve current pile location
        ld      B,      A               ; card number pointed at

        ; have we reached the bottom of the pile?
        ;-----------------------------------------------------------------------
        call    cards.getPilePool       ; get pool address from the pile
        ld      A,      [DE]            ; first byte is number of cards in pile
        dec     A                       ; (adjust for 1-based)
        
        ; compare A (number of cards) with B (current card)
        cp      B
        
        ; if they are not equal, the pointer has not reached
        ; the top of the pile (visually, bottom on screen)
        ret     z

        ld      A,      B
        inc     A
        jp      pointToCard


moveCursorUp:
;===============================================================================
; move the cursor up, either going through a pile of cards,
; or upwards to the nearest pile above the current one
;-------------------------------------------------------------------------------
        ; retrieve current pile location (HL) and card number pointed at (A)
        call    getCursorPile
        and     A,      A               ; (set flags!)
        
        ; is the current position 0? i.e. at the
        ; bottom (top, visually) of the pile?
        ret     z

        dec     A
        jp      pointToCard